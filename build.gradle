plugins {
    id 'java'
    id 'org.jreleaser' version '1.15.0' // ver 1.15.0 has no dir not exist bug
    id 'java-gradle-plugin'
    id 'idea'
    id 'eclipse'
    id 'groovy'
    alias(libs.plugins.kotlin)
    alias(libs.plugins.spotless)
    alias(libs.plugins.retry)
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "21"
    }
}

group = 'dev.aoqia'
def baseVersion = '0.1.0'

def ENV = System.getenv()
if (!ENV.MAVEN_USERNAME) {
    version = baseVersion + '.local'
}

// We must build against the version of Kotlin Gradle ships with.
def props = new Properties()
Project.class.getClassLoader().getResource("gradle-kotlin-dsl-versions.properties").openStream().withCloseable {
    props.load(it)
}
def kotlinVersion = props.getProperty("kotlin")
if (libs.versions.kotlin.get() != kotlinVersion) {
    throw new IllegalStateException("Requires Kotlin version: ${kotlinVersion}")
}

repositories {
    maven {
        name = 'Fabric'
        url = 'https://maven.fabricmc.net/'
    }
    mavenCentral()
}

configurations {
    bootstrap {
        transitive false
    }
    compileClasspath.extendsFrom bootstrap
    runtimeClasspath.extendsFrom bootstrap
    testRuntimeClasspath.extendsFrom bootstrap
}

configurations.configureEach {
    resolutionStrategy {
        failOnNonReproducibleResolution()
    }

    if (canBeConsumed) {
        attributes {
            attribute(GradlePluginApiVersion.GRADLE_PLUGIN_API_VERSION_ATTRIBUTE,
                objects.named(GradlePluginApiVersion, GradleVersion.current().getVersion()))
        }
    }
}

sourceSets {
    commonDecompiler {
        java {
            srcDir("src/decompilers/common")
        }
    }
    fernflower {
        java {
            srcDir("src/decompilers/fernflower")
        }
    }
    cfr {
        java {
            srcDir("src/decompilers/cfr")
        }
    }
    vineflower {
        java {
            srcDir("src/decompilers/vineflower")
        }
    }
}

dependencies {
    implementation gradleApi()

    bootstrap project(":bootstrap")

    // libraries
    implementation libs.commons.io
    implementation libs.gson
    implementation libs.guava
    implementation libs.bundles.asm

    // game handling utils
    implementation(libs.fabric.stitch) {
        exclude module: 'enigma'
    }

    // tinyfile management
    implementation libs.fabric.tiny.remapper
    implementation libs.fabric.access.widener
    implementation libs.fabric.mapping.io
    implementation(libs.fabric.lorenz.tiny) {
        transitive = false
    }

    implementation libs.fabric.loom.nativelib

    // decompilers
    fernflowerCompileOnly runtimeLibs.fernflower
    fernflowerCompileOnly libs.fabric.mapping.io

    cfrCompileOnly runtimeLibs.cfr
    cfrCompileOnly libs.fabric.mapping.io

    vineflowerCompileOnly runtimeLibs.vineflower
    vineflowerCompileOnly libs.fabric.mapping.io

    fernflowerApi sourceSets.commonDecompiler.output
    cfrApi sourceSets.commonDecompiler.output
    vineflowerApi sourceSets.commonDecompiler.output

    implementation sourceSets.commonDecompiler.output
    implementation sourceSets.fernflower.output
    implementation sourceSets.cfr.output
    implementation sourceSets.vineflower.output

    // source code remapping
    implementation libs.fabric.mercury

    // Kotlin
    implementation(libs.kotlin.metadata) {
        transitive = false
    }

    // Kapt integration
    compileOnly libs.kotlin.gradle.plugin

    // Testing
    testImplementation(gradleTestKit())
    testImplementation(testLibs.spock) {
        exclude module: 'groovy-all'
    }
    testImplementation testLibs.junit.jupiter.engine
    testRuntimeOnly testLibs.junit.platform.launcher
    testImplementation(testLibs.javalin) {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation testLibs.mockito
    testImplementation testLibs.java.debug

    compileOnly runtimeLibs.jetbrains.annotations
    testCompileOnly runtimeLibs.jetbrains.annotations

    testCompileOnly(testLibs.mixin) {
        transitive = false
    }
}

jar {
    manifest {
        attributes 'Implementation-Version': project.version
    }

    from configurations.bootstrap.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    from sourceSets.commonDecompiler.output.classesDirs
    from sourceSets.cfr.output.classesDirs
    from sourceSets.fernflower.output.classesDirs
    from sourceSets.vineflower.output.classesDirs
}

base {
    archivesName = project.name
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

spotless {
    setLineEndings(com.diffplug.spotless.LineEnding.UNIX)

    java {
        targetExclude("**/loom/util/DownloadUtil.java")
        targetExclude("**/generated/**")
        removeUnusedImports()
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
        //palantirJavaFormat()
    }

    groovy {
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
        importOrder('java', 'javax', '', 'net.fabricmc', '', 'dev.aoqia', '\\#')
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
        greclipse()
    }

    groovyGradle {
        target('src/**/*.gradle', '*.gradle')
        // Exclude build.gradle because it keeps pestering me about it!
        targetExclude("**/build.gradle")
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
        greclipse()
    }

    kotlin {
        indentWithSpaces(4)
        trimTrailingWhitespace()
        endWithNewline()
        licenseHeaderFile(rootProject.file("HEADER")).yearSeparator("-")
        targetExclude("**/build.gradle.kts")
        targetExclude("src/test/resources/projects/*/**")
        ktlint()
    }
}

//codenarc {
//    toolVersion = libs.versions.codenarc.get()
//    configFile = file("codenarc.groovy")
//}

gradlePlugin {
    plugins {
        leafLoom {
            id = 'leaf-loom'
            implementationClass = 'dev.aoqia.loom.bootstrap.LoomGradlePluginBootstrap'
        }
    }
}

test {
    maxHeapSize = "2560m"
    jvmArgs "-XX:+HeapDumpOnOutOfMemoryError"
    useJUnitPlatform()

    // Forward system prop onto tests.
    if (System.getProperty("leaf.loom.test.homeDir")) {
        systemProperty "leaf.loom.test.homeDir", System.getProperty("leaf.loom.test.homeDir")
    }


    if (ENV.CI) {
        retry {
            maxRetries = 3
        }
    }
}

// Workaround https://github.com/gradle/gradle/issues/25898
tasks.withType(Test).configureEach {
    jvmArgs = [
        '--add-opens=java.base/java.lang=ALL-UNNAMED',
        '--add-opens=java.base/java.util=ALL-UNNAMED',
        '--add-opens=java.base/java.lang.invoke=ALL-UNNAMED',
        '--add-opens=java.base/java.net=ALL-UNNAMED'
    ]
}


import org.gradle.api.internal.artifacts.configurations.ConfigurationRoles
import org.gradle.util.GradleVersion
import org.w3c.dom.Document
import org.w3c.dom.Element
import org.w3c.dom.Node

//publishing {
//    publications {
//        if (!ENV.EXPERIMENTAL) {
//            // Also publish a snapshot so people can use the latest version if they wish
//            snapshot(MavenPublication) { publication ->
//                groupId project.group
//                artifactId project.base.archivesName.get()
//                version baseVersion + '-SNAPSHOT'
//
//                from components.java
//            }
//
//            // Manually crate the plugin marker for snapshot versions
//            snapshotPlugin(MavenPublication) { publication ->
//                groupId 'leaf-loom'
//                artifactId 'leaf-loom.gradle.plugin'
//                version baseVersion + '-SNAPSHOT'
//
//                pom.withXml({
//                    // Based off org.gradle.plugin.devel.plugins.MavenPluginPublishPlugin
//                    Element root = asElement()
//                    Document document = root.getOwnerDocument()
//                    Node dependencies = root.appendChild(document.createElement('dependencies'))
//                    Node dependency = dependencies.appendChild(document.createElement('dependency'))
//                    Node groupId = dependency.appendChild(document.createElement('groupId'))
//                    groupId.setTextContent(group)
//                    Node artifactId = dependency.appendChild(document.createElement('artifactId'))
//                    artifactId.setTextContent('leaf-loom')
//                    Node version = dependency.appendChild(document.createElement('version'))
//                    version.setTextContent(baseVersion + '-SNAPSHOT')
//                })
//            }
//        }
//    }
//}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        passphrase = ENV.GPG_PASSPHRASE_KEY
        publicKey = ENV.GPG_PUBLIC_KEY
        secretKey = ENV.GPG_PRIVATE_KEY
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    applyMavenCentralRules = true
                    active = 'ALWAYS'
                    snapshotSupported = true
                    username = ENV.MAVEN_USERNAME
                    password = ENV.MAVEN_PASSWORD
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}

// Need to tweak this file to pretend we are compatible with j8 so the bootstrap will run.
tasks.withType(GenerateModuleMetadata).configureEach {
    doLast {
        def file = outputFile.get().asFile

        def metadata = new groovy.json.JsonSlurper().parseText(file.text)

        metadata.variants.each {
            it.attributes["org.gradle.jvm.version"] = 8
        }

        file.text = groovy.json.JsonOutput.toJson(metadata)
    }
}

// A task to output a json file with a list of all the test to run
tasks.register('writeActionsTestMatrix') {
    doLast {
        def testMatrix = []
        file('src/test/groovy/dev/aoqia/loom/test/integration').eachFile {
            if (it.name.endsWith("Test.groovy")) {
                if (it.name.endsWith("ReproducibleBuildTest.groovy")) {
                    // This test gets a special case to run across all os's
                    return
                }

                if (it.name.endsWith("DebugLineNumbersTest.groovy")) {
                    // Known flakey test
                    return
                }

                def className = it.name.replace(".groovy", "")
                testMatrix.add("dev.aoqia.loom.test.integration.${className}")
            }
        }

        // Run all the unit tests together
        testMatrix.add("dev.aoqia.loom.test.unit.*")

        // Kotlin tests
        testMatrix.add("dev.aoqia.loom.test.kotlin.*")

        def json = groovy.json.JsonOutput.toJson(testMatrix)
        def output = file("build/test_matrix.json")
        output.parentFile.mkdir()
        output.text = json
    }
}

tasks.named('wrapper') {
    distributionType = Wrapper.DistributionType.ALL
}

/**
 * Run this task to download the gradle sources next to the api jar, you may need to manually attach the sources jar
 */
tasks.register('downloadGradleSources') {
    doLast {
        // Awful hack to find the gradle api location
        def gradleApiFile = project.configurations.detachedConfiguration(dependencies.gradleApi()).files.stream()
            .find {
                it.name.startsWith("gradle-api")
            }

        def gradleApiSources = new File(gradleApiFile.absolutePath.replace(".jar", "-sources.jar"))
        def url = "https://services.gradle.org/distributions/gradle-${GradleVersion.current().getVersion()}-src.zip"

        gradleApiSources.delete()

        println("Downloading (${url}) to (${gradleApiSources})")
        gradleApiSources << new URL(url).newInputStream()
    }
}

tasks.register('printActionsTestName', PrintActionsTestName) {
}

/**
 * Replaces invalid characters in test names for GitHub Actions artifacts.
 */
abstract class PrintActionsTestName extends DefaultTask {
    @Input
    @Option(option = "name", description = "The test name")
    String testName

    @TaskAction
    def run() {
        def sanitised = testName.replace('*', '_')
        new File(System.getenv().GITHUB_OUTPUT) << "\ntest=$sanitised"
    }
}

apply from: rootProject.file('gradle/versions.gradle')
